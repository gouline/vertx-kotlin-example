buildscript {
    ext {
        kotlin_version = '1.1.0-rc-91'
        vertx_version = '3.4.0.Beta1'

        // Kotlin 1.1 EAP
        kotlinEapUrl = 'http://dl.bintray.com/kotlin/kotlin-eap-1.1'
    }

    repositories {
        maven { url kotlinEapUrl }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'kotlin'

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'net.gouline.vertxexample.MainVerticle'

repositories {
    maven { url kotlinEapUrl }
    jcenter()
}

dependencies {
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

// Redeploy watcher.
run {
    args = ['run', mainVerticleName,
            "--launcher-class=$mainClassName",
            "--redeploy=src/**/*.*",
            "--on-redeploy=./gradlew classes"
    ]
}

// Naming and packaging settings for the "shadow jar".
shadowJar {
    baseName = 'app'
    classifier = 'shadow'

    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

// Heroku relies on the 'stage' task to deploy.
task stage {
    dependsOn shadowJar
}
